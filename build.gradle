plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.8"
    id 'net.neoforged.moddev' version '2.0.76'
    id "me.modmuss50.mod-publish-plugin" version "0.8.3"
}

group = 'dev.ftb.mods'
version = mod_version

base {
    archivesName = "ftb-pause-menu-api"
}

apply from: 'https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/changelog.gradle'
apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/publishing.gradle"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

neoForge {
    version = project.mod_loader_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

     accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://maven.ftb.dev/releases"
        content {
            includeGroup("dev.ftb.mods")
        }
    }
    maven {
        url = "https://maven.architectury.dev"
        content {
            includeGroup("dev.architectury")
        }
    }
    // flatfile libs
    flatDir(dir: 'libs')
}

dependencies {
    def tipsEnabled = true
    def justZoomEnabled = true

    if (tipsEnabled) {
        implementation "curse.maven:tips-306549:5844057"
        runtimeOnly "curse.maven:prickle-1023259:5836410"
        runtimeOnly "curse.maven:bookshelf-228525:6148170"
    }

    if (justZoomEnabled) {
        implementation "curse.maven:just-zoom-561885:5619666"
        runtimeOnly "curse.maven:konkrete-410295:5453385"
    }
}

processResources {
    var replaceProperties = [
            mod_version: project.version,
            minecraft_version      : project.properties.minecraft_version,
            minecraft_version_range: project.properties.minecraft_version_range,
            neo_version            : project.properties.mod_loader_version,
            neo_version_range      : project.properties.mod_loader_version_range,
            loader_version_range   : project.properties.loader_version_range,
    ]

    inputs.properties replaceProperties

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

// Create a jar just containing the api package
tasks.register('apiJar', Jar) {
    archiveClassifier.set('api')
    from sourceSets.main.output
    include 'dev/ftb/mods/pmapi/api/**'
}

tasks.named('jar').configure {
    dependsOn 'apiJar'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
            artifact apiJar
        }
    }

    repositories {
        if (ftbPublishing.ftbToken) {
            maven {
                url ftbPublishing.ftbURL
                credentials {
                    username = ftbPublishing.ftbUser
                    password = ftbPublishing.ftbToken
                }
            }
        }
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

publishMods {
    def cfToken = providers.environmentVariable("CURSEFORGE_KEY")

    dryRun = !cfToken.isPresent()
    changelog = createChangelog(project).trim()
    file = jar.archiveFile
    displayName = "[NEOFORGE] FTB Pause Menu API ${project.version}"
    modLoaders.add("neoforge")

    def tag = providers.environmentVariable("TAG").getOrElse("release")
    type = tag.endsWith("-beta") ? BETA : (tag.endsWith("-alpha") ? ALPHA : STABLE)

    curseforge {
        accessToken = cfToken.isPresent() ? cfToken.get() : ""
        minecraftVersions.add(minecraft_version)

        projectId = curseforge_id
        additionalFiles.from(apiJar.archiveFile)
    }
}
